#version 330 core

layout(lines) in;
layout(triangle_strip, max_vertices = 40) out;

uniform bool  measure_line_width_in_pixel = true;
uniform float reference_line_width = 1.0;
uniform float blend_width_in_pixel = 1.0;
uniform float viewport_height = 1080.0;
uniform int   nr_samples;
uniform int   strip_length;

in LINE_GS {
	int   instance_idx;
	vec4  position;
	vec3  normal_eye;
	float line_width;
	vec4  color;
	vec4  halo_color;
	float halo_width_in_pixel;
	float percentual_halo_width;
	float depth_offset;
} gi[];

out LINE_FS {
	vec4  color;
	vec4  halo_color;
	vec3  side_vector_eye;
	vec3  center_position_eye;
	vec3  position_eye;
	float percentual_blend_width;
	float percentual_core_size;
	float percentual_point_size;
	float percentual_splat_size;
	float depth_offset;
} fo;

//***** begin interface of view.glsl ***********************************
mat4 get_modelview_matrix();
mat4 get_projection_matrix();
mat4 get_modelview_projection_matrix();
mat4 get_inverse_modelview_matrix();
mat4 get_inverse_modelview_projection_matrix();
mat3 get_normal_matrix();
mat3 get_inverse_normal_matrix();
//***** end interface of view.glsl ***********************************

//***** begin interface of splat.glsl ***********************************
void prepare_splat(
	in float reference_point_radius, in float pixel_extent,
	in float blend_width_in_pixel, in float halo_width_in_pixel, in float percentual_halo_width,
	out float percentual_core_size, out float percentual_point_size,
	out float percentual_blend_width, out float percentual_splat_size);
//***** end interface of surface.glsl ***********************************

//***** begin interface of hyperbolic.glsl ***********************************
vec4 hyperbolic_project(in vec4 p);
vec4 hyperbolic_transform(in vec4 x);
vec4 hyperbolic_mixture(in vec4 x, float t, in vec4 y);
//***** end interface of hyperbolic.glsl ***********************************

void emit_corner(in vec3 pos_eye, in float param)
{
	fo.position_eye = pos_eye;
	gl_Position	= get_projection_matrix() * vec4(pos_eye,1.0);
	EmitVertex();
}

void emit_edge(in vec3 position_eye, in vec3 dir_eye, int i, float width)
{
	if (i == 0)
		fo.color = gi[i].color;
	else
		fo.color = vec4(0.0, 0.0, 0.0, 1.0);
	fo.depth_offset = gi[i].depth_offset;
	fo.halo_color = gi[i].halo_color;
	vec3 side_dir_eye = normalize(cross(gi[i].normal_eye, dir_eye));
	float pixel_extent = 2.0*abs(position_eye.z/(viewport_height*get_projection_matrix()[1][1]));
	float reference_width = 0.5*width*gi[i].line_width*(measure_line_width_in_pixel ? pixel_extent : reference_line_width);
	
	prepare_splat(reference_width, pixel_extent, blend_width_in_pixel, gi[i].halo_width_in_pixel, gi[i].percentual_halo_width,
				  fo.percentual_core_size, fo.percentual_point_size,fo.percentual_blend_width, fo.percentual_splat_size);

	fo.side_vector_eye = reference_width*side_dir_eye;
	vec3 d_eye = fo.percentual_splat_size*reference_width*side_dir_eye;
	fo.center_position_eye = position_eye;
	emit_corner(position_eye + d_eye,  fo.percentual_splat_size);
	emit_corner(position_eye - d_eye, -fo.percentual_splat_size);
}

void main()
{
	if (gi[0].depth_offset > 100000.0)
		return;
	if (gi[1].depth_offset > 100000.0)
		return;
	vec4 p0 = hyperbolic_transform(gi[0].position);
	vec4 p1 = hyperbolic_transform(gi[1].position);
	float t0 = float(gi[0].instance_idx) / nr_samples;
	float s = 1.0f / (nr_samples*strip_length);
	vec4 q0 = hyperbolic_project(hyperbolic_mixture(p0, t0, p1));
	vec3 p_eye_start = (get_modelview_matrix()*vec4(q0.xyz, 1.0)).xyz;
	vec3 p_eye_end;
	vec3 tangent_eye;
	for (int j = 1; j <= strip_length; ++j) {
		float l = t0 + s * j;
		vec4 q1 = hyperbolic_project(hyperbolic_mixture(p0, l, p1));
		p_eye_end = (get_modelview_matrix()*vec4(q1.xyz,1.0)).xyz;
		tangent_eye = p_eye_end-p_eye_start;
		emit_edge(p_eye_start, tangent_eye, 0, q0.w);
		q0 = q1;
		p_eye_start = p_eye_end;
	}
	emit_edge(p_eye_start, tangent_eye, 0, q0.w);
}